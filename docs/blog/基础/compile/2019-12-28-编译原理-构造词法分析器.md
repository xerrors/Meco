---
title: 编译原理-构造词法分析器
permalink: /lexical-analysis
date: 2019-12-28 00:04:58
tags: 
 - 基础
 - 编译
categories: 基础
zhuanlan: compile
next:
  title: 手动构造词法分析器
  path: /lexical-analysis-2/
---

这篇文章是记录我在学习编译原理实验的时候遇到的一些问题以及自己的做法。记录一下自己踩过的坑！第一个题目比较简单，是基于 flex 构造词法分析器，只要掌握一些 flex 的基础就可以完成。

<!-- more -->

在开始之前先分享一个我认为比较好的 flex 的教程，

[《Flex & Bison》 比较贵，不深入学习不建议](https://item.jd.com/64141579139.html)

[南京大学-词法分析和语法分析课件](https://cs.nju.edu.cn/daixinyu/1006.pdf)

[windows平台下flex下载地址](https://sourceforge.net/projects/winflexbison/)

这个课件可以说是科班学生做实验的时候的一份极好的材料了，里面不仅包含词法分析器所需要用到的 flex 教程，也包含后面语法分析所需要的 bison 教程，值得一读！

> 题目一

【问题描述】设计c语言常见单词的正规式，编制`lex`源文件，利用flex编译得到词法分析的.c文件，继而对该文件编译得到词法分析器。

【输入形式】输入一段c语言程序

【输出形式】各类单词的token字，或者给出程序中的单词错误。

这次的实验算是简单一点的，只需要掌握一些 Flex 的基本使用方法就可以完成。

## flex 的安装以及基础知识

GNU Flex在Linux下的安装非常简单。你可以去它的官方网站上下载安装包自行安装，不过在基于Debian的Linux系统下，更简单的安装方法是直接在命令行敲入如下命令：

```shell
sudo apt-get install flex
```
在 Windows 平台之下也可以使用 `flex` 的替代品：`win_flex` 来完成，首先是下载上面的链接的文件，下载之后使用方法应该就跟 `Linux` 下差不多了。

Flex 文件以`.l` 或者 `.lex` 结尾，Flex源代码文件包括三个部分，由`%%`隔开，如下所示：

```
{definitions}           // 定义部分
%%
{rules}                 // 规则部分
%%
{user subroutines}      // 用户自定义代码部分
```

### 第一部分

第一部分为辅助定义部分，实际上就是给某些后面可能经常用到的正则表达式取一个别名，从而简化词法规则的书写。定义部分的格式一般为：

```
name definition
```

其中 `name` 是名字，`definition` 是任意的正则表达式；例如，下面的这段代码定义了两个名字： `digit` 和 `letter`，前者代表0到9中的任意一个数字字符，后者则代表任意一个小写字母、大写字母或下划线：

```
…
digit [0-9]
letter [_a-zA-Z]
%%
…
%%
…
```

值得一提的是，如果用户想要对这部分所用到的变量、函数或者头文件进行声明，可以定义部分之前使用`%{`和`%}`符号将要声明的内容添加进去。被`%{`和`%}`所包围的内容也会一并拷贝到`lex.yy.c`的最前面。

### 第二部分

第二部分为规则部分，它由正则表达式和相应的响应函数组成，其格式为：

```
pattern {action}
```

其中 `pattern` 为正则表达式，其书写规则与前面的定义部分的正则表达式相同。而 `action` 则为将要进行的具体操作，这些操作可以用一段C代码表示。Flex 将按照这部分给出的内容依次尝试每一个规则，尽可能匹配最长的输入串。如果有些内容不匹配任何规则，Flex 默认只将其拷贝到标准输出，想要修改这个默认行为的话只需要在所有规则的最后加上一条 `'.'`（即匹配任何输入）规则，然后在其对应的 `action` 部分书写你想要的行为即可。

例如，下面这段Flex代码在遇到输入文件中包含一串数字时，会将该数字串转化为整数值
并打印到屏幕上：

```
…
digit [0-9]
%%
{digit}+ { printf("Integer value %d\n", atoi(yytext)); }
…
%%
…
```

其中变量 `yytext` 的类型为 `char*` ，它是 Flex 为我们提供的一个变量，里面保存了当前词法单元所对应的词素。函数 `atoi()` 的作用是把一个字符串表示的整数转化为 `int` 类型。

### 第三部分

第三部分为用户子程序部分，这部分代码会被原封不动地拷贝到`lex.yy.c`中，以方便用户自定义所需要执行的函数，main函数也可以写在这里。

## 解题思路

### Level1

一个简单的 lex 代码如下：

```c
/* test.lex */
%option main
%{
    int line=1;
%}
DIGIT [0-9]
OINTEGER [1-9]{DIGIT}*
INTEGER ("+"|"-")?{OINTEGER}
DECIMAL {INTEGER}(.{OINTEGER})?
LETTER [a-zA-Z]
ID ({LETTER}|"_")({LETTER}|"_"|{DIGIT})*
OPT ("+="|"-="|"*="|"/="|"+"|"-"|"*"|"/"|">="|"<="|"=="|"="|">"|"<")
%%
\n {++line;}
(int|float|double|short) {printf("line%d:(type,%s)\n", line, yytext);}
for {printf("line%d:(for,)\n", line);}
{INTEGER} {printf("line%d:(integer,%s)\n",line, yytext);}
{DECIMAL} {printf("line%d:(decimal,%s)\n",line, yytext);}
{ID} {printf("line%d:(identify,%s)\n",line, yytext);}
("("|")"|"{"|"}"|"["|"]") {printf("line%d:(bracket,\"%s\")\n",line, yytext);}
"\"" {printf("line%d:(QUOTE,)\n", line);}
{OPT} {printf("line%d:(OPT,%s)\n",line,yytext);}
. {}
%%
/*辅助*/
```

在 Windows 平台下使用的是 win-flex （在文件所在的路径下执行下面的命令）

```shell
win_flex test.lex 
```

然后就会得到 `lex.yy.c` 再然后编译一下就可以了

```shell
gcc lex.yy.c
```

当然上面的步骤也可以使用 VS 或者 `Code Blocks` 、`Xcode` 等来编译。最终会得到一个可执行文件，`lex.yy`，最后一步就是测试一下。新建一个文件 1.c

```c
int main(){
    int a = 10;
    double b = 20.9;
    if(a <= b)
        a+=b;
    return a;
}
```

然后执行：

```bash
C:\win-flex>lex.yy < 1.c
line1:(type,int)
line1:(identify,main)
line1:(bracket,"(")
line1:(bracket,")")
line1:(bracket,"{")
line2:(type,int)
line2:(identify,a)
line2:(OPT,=)
line2:(integer,10)
line3:(type,double)
line3:(identify,b)
line3:(OPT,=)
line3:(decimal,20.9)
line4:(identify,if)
line4:(bracket,"(")
line4:(identify,a)
line4:(OPT,<=)
line4:(identify,b)
line4:(bracket,")")
line5:(identify,a)
line5:(OPT,+=)
line5:(identify,b)
line6:(identify,return)
line6:(identify,a)
line7:(bracket,"}")
```

### Level2

上面的例子比较简单，只是了解下 lex 的基础语法，那么当遇到文件里面有注释的应该怎么识别呢？比如：

```c
// 2.c
int main()
{
   float i_1 = 1.005e-3;
   /* this is remark*/
}
```

这个例子不但有了一个注释，还多了一个数据格式。科学计数法的单精度浮点数。

单精度浮点数可以使用这样：

```
DECIMAL {INTEGER}(.(0)*{OINTEGER})?
FLOAT {DECIMAL}(e{INTEGER})
```

这里修改了上面对 DECIMAL 的定义，这样就可以识别类似于 `20.0009` 的数字了，再利用float的特点就很好理解了。

**单行评论**

这个很容易,因为在lex / flex中.将不符合换行符.因此,以下内容将从//到行的末尾匹配,然后不执行任何操作.

```
"//".*                                    { /* DO NOTHING */ }
```

**多行评论**

这有点棘手，而且`*`是正则表达式字符以及注释标记的关键部分这一事实使得以下正则表达式有点难以阅读。我使用`[*]`作为识别字符`*`的模式；在flex / lex中,您可以使用`“*”`代替。使用您认为更具可读性的任何一种。本质上，正则表达式匹配以(字符串) * 结尾的字符序列，直到找到下一个字符为/的字符串。换句话说，它具有与C代码相同的逻辑.

```
[/][*][^*]*[*]+([^*/][^*]*[*]+)*[/]       { /* DO NOTHING */ }
```

以上要求终止* /；未终止的注释将强制词法分析器返回到注释的开头并接受一些其他令牌，通常是/ division运算符。这可能不是你想要的，但是从未经评论的评论中恢复并不容易，因为没有真正好的方法可以知道评论应该在哪里结束。因此，我建议添加错误规则：

```
[/][*][^*]*[*]+([^*/][^*]*[*]+)*[/]       { /* DO NOTHING */ }
[/][*]                                    { fatal_error("Unterminated comment"); }
```

现在就可以完美的运行了

```bash
C:\Tools\win-flex>win_flex.exe 1104.lex && gcc lex.yy.c -o lex.yy && lex.yy < 2.c
line1:(type,int)
line1:(identify,main)
line1:(bracket,"(")
line1:(bracket,")")
line2:(bracket,"{")
line3:(type,float)
line3:(identify,i_1)
line3:(OPT,=)
line3:(float,1.005e-3)
line5:(bracket,"}")
```

### Level3

比如这一个？

```c
// 4.c
void main(){
    int a, b, c;
    char d;
    float e;
    scanf("%d%d%d%c",&a,&b,&c,&d);
    if(a <= b){
        a+=b;
        printf("%d",a);}
    else
        printf("%c",d);
    e = sqrt(abs(b));
    return e;
}
```

首先是添加之前没有识别的 void char 等等，话不多说最后结果，也很好理解。

```c
%option main
%{
    int line=1;
%}
DIGIT [0-9]
OINTEGER [1-9]{DIGIT}*
INTEGER ("+"|"-")?{OINTEGER}
DECIMAL {INTEGER}(.(0)*{OINTEGER})?
FLOAT {DECIMAL}(e{INTEGER})
LETTER [a-zA-Z]
ID ({LETTER}|_)({LETTER}|_|{DIGIT})*
OPT ("+="|"-="|"*="|"/="|"+"|"-"|"*"|"/"|">="|"<="|"=="|"="|">"|"<")
KEYWORD ("if"|"else"|"scanf"|"for"|"printf"|"return"|"sqrt"|"abs")
TI [%|&]{LETTER}
%%
\n {++line;}
(void|int|float|double|short|char) {printf("line%d:(type, %s)\n", line, yytext);}
{INTEGER} {printf("line%d:(integer, %s)\n",line, yytext);}
{DECIMAL} {printf("line%d:(decimal, %s)\n",line, yytext);}
{FLOAT} {printf("line%d:(float, %s)\n",line, yytext);}
{KEYWORD} {printf("line%d:(keyword, %s)\n",line,yytext);}
{ID} {printf("line%d:(identify, %s)\n",line, yytext);}
("("|")"|"{"|"}"|"["|"]"|"\""|","|";") {printf("line%d:(bracket, %s)\n",line, yytext);}
{OPT} {printf("line%d:(OPT, %s)\n",line,yytext);}
{TI} {printf("line%d:(typeidentify, %s)\n",line,yytext);}
"//".*  {}
[/][*][^*]*[*]+([^*/][^*]*[*]+)*[/]       {}
. {}
%%
/*辅助*/
```

### Level4

这里也就是最后一个等级了，就在这里再详细的每一个部分详细的介绍一下吧！

首先是把用户自定义部分代码给写出来，我们放在程序的开始：

```c
%option noyywrap
%{
    #include <stdio.h>
    #include <stdlib.h>
    #include <string.h>
    int line=1;    
%}
```

上面就是一些相关的库的引用，同时定义了一个`line` 来记录行号。上面的 `option noyywrap`的意思是这个Flex程序中不含 `yywrap`函数，这样后面就不会报错了。

后面就是定义一些正则表达式了，这里需要识别的正则有一下几种

```
DIGIT [0-9]
OINTEGER [1-9]{DIGIT}*					// 正整数
INTEGER ("+"|"-")?({OINTEGER}|0)		// 整数
DECIMAL {INTEGER}(.(0)*{OINTEGER})?		// 数字
FLOAT {DECIMAL}(e{INTEGER})				// 科学计数法的浮点数
LETTER [a-zA-Z]							// 字母
ID ({LETTER}|_)({LETTER}|_|{DIGIT})*	// 标识符
OPT ("+="|"-="|"*="|"/="|"+"|"-"|"*"|"/"|">="|"<="|"=="|"="|">"|"<")	// 操作符
KEYWORD ("if"|"else"|"scanf"|"for"|"printf"|"return"|"sqrt"|"abs"|"main"|"float")	// 关键字
TI [%|&]{LETTER} // 类型识别符
ERROR_FLOAT {DIGIT}*.{DIGIT}{DIGIT}*e	// 错误的浮点数（为了后面的错误识别与处理）
```

**第二部分就是规则部分：**

这里使用了一个`push() `的自定义函数（后面会介绍到），因为这个词法识别程序涉及到错误处理，当整个文件分析完成之后才会输出分析信息，所以在处理的时候就需要把分析的结果存储到一个链表里面（当然使用数组也是没有问题的），当没有问题的时候再输出，如果出现错误就输出错误信息。

```c
%%
\n {++line;}
(void|int|double|short|char) {push(line, "type", yytext);}	// 类型
{INTEGER} {push(line, "integer", yytext);}
{DECIMAL} {push(line, "decimal", yytext);}
{FLOAT} {push(line, "float", yytext);}
{ERROR_FLOAT} {error = 1; printf("Error at Line %d: Illegal floating point number \"%s\".\n", line, yytext);}
{KEYWORD} {push(line, "keyword", yytext);}
{ID} {push(line, "identify", yytext);}
("("|")"|"{"|"}"|"["|"]"|"\""|","|";") {push(line, "bracket", yytext);}	// 间隔符
{OPT} {push(line, "OPT", yytext);}
{TI} {push(line, "typeidentify", yytext);}
"//".*  {}
[/][*][^*]*[*]+([^*/][^*]*[*]+)*[/]       {}
. {}	// 默认什么也不处理
%%
```

所以在此基础上要对第一部分的代码进行修改，定义了一个结构体：

```c
typedef struct Msg {
    int line;
    char token[100];
    char word[100];
    struct Msg* next;
}Msg, *pMsg;
```

这个链表看着简单，但是对于写惯了Python和前端语言的我已经忘记怎样去写链表了，，，靠着之前的知识储备还是把链表写完了看不懂的话就要去复习一下数据结构的知识咯），可以根据分析的大小来申请内存，效率更高。写完之后，这样就可以在扫描的时候把输出信息保存到一个链表里面，然后直到最后来判断之前是否输出了错误信息，如果输出了错误信息后面的信息就不输出了，否则就输出。

```
%{
    #include <stdio.h>
    #include <stdlib.h>
    #include <string.h>
    int line=1;    
    typedef struct Msg {
        int line;
        char token[100];
        char word[100];
        struct Msg* next;
    }Msg, *pMsg;

    pMsg m, p;
    int error = 0;

    void push(int line, const char* token, const char* word) {
        pMsg tmp = (pMsg)malloc(sizeof(Msg));
        tmp->line = line;
        strcpy(tmp->token, token);
        strcpy(tmp->word, word);
        p->next = tmp;
        p = p->next;
    }
%}
```

上面两个完成之后，就需要写**第三部分**的代码了，比如我们需要自己创建一个main函数，默认的main函数无法完成我们所需要的功能，这里提一下：`yylex()` 是 Flex 里面的词法分析函数，所以在main函数里面必须要包含这个函数，先来看一下 main 函数的样子：

```c
int main() {
    m = (pMsg)malloc(sizeof(Msg));
    p = m;
    yyin=stdin;
    yylex();
    printmsg();
}
```

第3、4行代码是对链表进行初始化，第5行是把`yyin`定向位标准输入，最后调用`printmsg()`函数来执行输出。

```c
int printmsg() {
    if (error)
        return 0;
    p = m;
    p = p->next;
    while(p) {
        printf("line%d:(%s, %s)\n", p->line, p->token, p->word);
        p = p->next;
    }
}
```

### 代码详情

```c
%option noyywrap
%{
    #include <stdio.h>
    #include <stdlib.h>
    #include <string.h>
    int line=1;    
    typedef struct Msg {
        int line;
        char token[100];
        char word[100];
        struct Msg* next;
    }Msg, *pMsg;

    pMsg m, p;
    int error = 0;

    void push(int line, const char* token, const char* word) {
        pMsg tmp = (pMsg)malloc(sizeof(Msg));
        tmp->line = line;
        strcpy(tmp->token, token);
        strcpy(tmp->word, word);
        p->next = tmp;
        p = p->next;
    }
%}
DIGIT [0-9]
OINTEGER [1-9]{DIGIT}*
INTEGER ("+"|"-")?({OINTEGER}|0)
DECIMAL {INTEGER}(.(0)*{OINTEGER})?
FLOAT {DECIMAL}(e{INTEGER})
LETTER [a-zA-Z]
ID ({LETTER}|_)({LETTER}|_|{DIGIT})*
OPT ("+="|"-="|"*="|"/="|"+"|"-"|"*"|"/"|">="|"<="|"=="|"="|">"|"<")
KEYWORD ("if"|"else"|"scanf"|"for"|"printf"|"return"|"sqrt"|"abs"|"main"|"float")
TI [%|&]{LETTER}
ERROR_FLOAT {DIGIT}*.{DIGIT}{DIGIT}*e
%%
\n {++line;}
(void|int|double|short|char) {push(line, "type", yytext);}
{INTEGER} {push(line, "integer", yytext);}
{DECIMAL} {push(line, "decimal", yytext);}
{FLOAT} {push(line, "float", yytext);}
{ERROR_FLOAT} {error = 1; printf("Error at Line %d: Illegal floating point number \"%s\".\n", line, yytext);}
{KEYWORD} {push(line, "keyword", yytext);}
{ID} {push(line, "identify", yytext);}
("("|")"|"{"|"}"|"["|"]"|"\""|","|";") {push(line, "bracket", yytext);}
{OPT} {push(line, "OPT", yytext);}
{TI} {push(line, "typeidentify", yytext);}
"//".*  {}
[/][*][^*]*[*]+([^*/][^*]*[*]+)*[/]       {}
. {}
%%
int printmsg() {
    if (error)
        return 0;
    p = m;
    p = p->next;
    while(p) {
        printf("line%d:(%s, %s)\n", p->line, p->token, p->word);
        p = p->next;
    }
}

int main() {
    m = (pMsg)malloc(sizeof(Msg));
    p = m;
    yyin=stdin;
    yylex();
    printmsg();
}
```


## 参考链接

> [[1] 南京大学-词法分析和语法分析课件](https://cs.nju.edu.cn/daixinyu/1006.pdf)
>
> [2]  《flex & bision》，John Levine著，陆军译，东南大学出版社，2011年。 